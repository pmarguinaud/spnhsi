SUBROUTINE SPCSIDG_PART1 (YDGEOMETRY, YDDYN, KSTA, KEND, PSDIVP,&
         &PSPDIVP,KMLOCSTA,KMLOCEND,LDSIDG,KSZNISNAX,NISNAX,SIHEG,SIHEG2,LDACC)

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1     , ONLY : JPIM, JPRB
USE YOMHOOK      , ONLY : LHOOK, DR_HOOK, JPHOOK
USE YOMDYN       , ONLY : TDYN
USE YOMMP0       , ONLY : MYSETN

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TDYN)        ,INTENT(IN)    :: YDDYN
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTA
INTEGER(KIND=JPIM),INTENT(IN)    :: KEND
INTEGER(KIND=JPIM),INTENT(IN)    :: KMLOCSTA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KMLOCEND
LOGICAl           ,INTENT(IN)    :: LDSIDG
REAL(KIND=JPRB),   INTENT(INOUT) :: PSDIVP(KSTA:KEND,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB),   INTENT(INOUT) :: PSPDIVP(KSTA:KEND,YDGEOMETRY%YRDIMV%NFLEVG)
INTEGER(KIND=JPIM),INTENT(IN)    :: KSZNISNAX
INTEGER(KIND=JPIM),INTENT(IN)    :: NISNAX(0:KSZNISNAX)
REAL(KIND=JPRB),   INTENT(IN)    :: SIHEG(YDGEOMETRY%YRDIMV%NFLEVG*&
                                          &YDGEOMETRY%YRMP%NSPEC2VF/2,3)
REAL(KIND=JPRB),   INTENT(IN)    :: SIHEG2(YDGEOMETRY%YRDIM%NSMAX+1,&
                                          &YDGEOMETRY%YRDIMV%NFLEVG,2:3)
LOGICAL, OPTIONAL, INTENT(IN)    :: LDACC
!     ------------------------------------------------------------------


INTEGER(KIND=JPIM):: II,JN,JV,IIS,II0
INTEGER(KIND=JPIM):: ISTA,IEND,JMLOC,IM,SIHEGSTA,SIHEGEND
LOGICAL           :: LLACC

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "mxture.h"
#include "spcsidg_part1acc.intfb.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SPCSIDG_PART1',0,ZHOOK_HANDLE)

ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDLAP=>YDGEOMETRY%YRLAP,YDMP=>YDGEOMETRY%YRMP)
ASSOCIATE(NSMAX=>YDDIM%NSMAX,NFLEVG=>YDDIMV%NFLEVG,&
        &SIHEGIND=>YDDYN%SIHEGIND,MYMS=>YDLAP%MYMS,NSPSTAF=>YDMP%NSPSTAF,NPTRMF=>YDMP%NPTRMF)

LLACC=.FALSE.
IF (PRESENT(LDACC)) LLACC=LDACC

IF (LLACC) THEN

  CALL SPCSIDG_PART1ACC (YDGEOMETRY, YDDYN, KSTA,KEND,PSDIVP,&
        &PSPDIVP,KMLOCSTA,KMLOCEND,LDSIDG,KSZNISNAX,NISNAX,SIHEG,SIHEG2)

ELSE


  IIS=2
  II0=1
  IF (.NOT. LDSIDG) THEN
    IIS=4
    II0=2
  ENDIF

  DO JMLOC=KMLOCSTA,KMLOCEND
  
  IM=MYMS(JMLOC)
  ISTA=NSPSTAF(IM)
  IEND=ISTA+2*(NISNAX(IM)+1)-1
  SIHEGSTA=SIHEGIND(JMLOC)
  SIHEGEND=SIHEGIND(JMLOC+1)-1

  IF (IM > 0) THEN
     II=IIS

!               Inversion of a symmetric matrix.

     IF (KMLOCSTA==KMLOCEND) THEN

       CALL MXTURE(NISNAX(IM)+1,IEND-ISTA+1,NFLEVG,NFLEVG,II,IIS,-2,1,&
       & SIHEG(SIHEGSTA:SIHEGEND,1),&
       & SIHEG(SIHEGSTA:SIHEGEND,2),&
       & SIHEG(SIHEGSTA:SIHEGEND,3),&
       & SIHEG(SIHEGSTA:SIHEGEND,2),&
       & SIHEG(SIHEGSTA:SIHEGEND,3),PSDIVP,PSPDIVP) 

     ELSE

       CALL MXTURE(NISNAX(IM)+1,IEND-ISTA+1,NFLEVG,NFLEVG,II,IIS,-2,1,&
       & SIHEG(SIHEGSTA:SIHEGEND,1),&
       & SIHEG(SIHEGSTA:SIHEGEND,2),&
       & SIHEG(SIHEGSTA:SIHEGEND,3),&
       & SIHEG(SIHEGSTA:SIHEGEND,2),&
       & SIHEG(SIHEGSTA:SIHEGEND,3),PSDIVP(ISTA:IEND,:),PSPDIVP(ISTA:IEND,:))

     ENDIF


    ELSE
  
    !               Inversion of a non-symmetric matrix.
      II=II0
  !à voir!    II=2 sans mise à 0 après
  
      IF (KMLOCSTA==KMLOCEND) THEN
  
        CALL MXTURE(NISNAX(IM)+1,IEND-ISTA+1,NFLEVG,NFLEVG,II,IIS,-2,3,&
         & SIHEG(SIHEGSTA:SIHEGEND,1),&
         & SIHEG(SIHEGSTA:SIHEGEND,2),&
         & SIHEG(SIHEGSTA:SIHEGEND,3),&
         & SIHEG2(:,:,2),&
         & SIHEG2(:,:,3),PSDIVP,PSPDIVP) 
  
  
      ELSE
  
        CALL MXTURE(NISNAX(IM)+1,IEND-ISTA+1,NFLEVG,NFLEVG,II,IIS,-2,3,&
         & SIHEG(SIHEGSTA:SIHEGEND,1),&
         & SIHEG(SIHEGSTA:SIHEGEND,2),&
         & SIHEG(SIHEGSTA:SIHEGEND,3),&
         & SIHEG2(:,:,2),&
         & SIHEG2(:,:,3),PSDIVP(ISTA:IEND,:),PSPDIVP(ISTA:IEND,:)) 
  
      ENDIF 
  
      IF (LDSIDG) THEN
  
        !!for KM=0 values with JI=2 are set to 0, as was the case in the original code
        !!(in the 49t0 version of SPCSI, array ZSPDIVG was initialized to zero, and the
        !!values with JI=2 for KM=0 were not computed)
        DO JV=1,NFLEVG
          DO JN=0,2*NSMAX,2
            PSPDIVP(ISTA+JN+1,JV)=0.0_JPRB
          ENDDO
        ENDDO
      ELSEIF (IIS==4) THEN
        DO JV=1,NFLEVG
          DO JN=0,NISNAX(IM)-1
            PSPDIVP(ISTA+4*JN+3,JV)=0.0_JPRB
            PSPDIVP(ISTA+4*JN+4,JV)=0.0_JPRB
          ENDDO
        ENDDO
      ENDIF
    ENDIF
  ENDDO

ENDIF

END ASSOCIATE
END ASSOCIATE

IF (LHOOK) CALL DR_HOOK('SPCSIDG_PART1',1,ZHOOK_HANDLE)
END SUBROUTINE SPCSIDG_PART1

