SUBROUTINE SPCSIDG_PART1 (YDGEOMETRY, YDDYN, KSTA, KEND, PSDIVP,&
         &PSPDIVP,KM,KMLOC,LDSIDG,KSZNISNAX,NISNAX,SIHEG,SIHEG2)

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1     , ONLY : JPIM, JPRB
USE YOMHOOK      , ONLY : LHOOK, DR_HOOK, JPHOOK
USE YOMDYN       , ONLY : TDYN

USE YOMMP0       , ONLY : MYSETN

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TDYN)        ,INTENT(IN)    :: YDDYN
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTA
INTEGER(KIND=JPIM),INTENT(IN)    :: KEND
INTEGER(KIND=JPIM),INTENT(IN)    :: KM 
INTEGER(KIND=JPIM),INTENT(IN)    :: KMLOC
LOGICAl           ,INTENT(IN)    :: LDSIDG
REAL(KIND=JPRB),   INTENT(INOUT) :: PSDIVP(KSTA:KEND,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB),   INTENT(INOUT) :: PSPDIVP(KSTA:KEND,YDGEOMETRY%YRDIMV%NFLEVG)
INTEGER(KIND=JPIM),INTENT(IN)    :: KSZNISNAX
INTEGER(KIND=JPIM),INTENT(IN)    :: NISNAX(0:KSZNISNAX)
REAL(KIND=JPRB),   INTENT(IN)    :: SIHEG(YDGEOMETRY%YRDIMV%NFLEVG*&
                                          &YDGEOMETRY%YRMP%NSPEC2VF/2,3)
REAL(KIND=JPRB),   INTENT(IN)    :: SIHEG2(YDGEOMETRY%YRDIM%NSMAX+1,&
                                          &YDGEOMETRY%YRDIMV%NFLEVG,2:3)
!     ------------------------------------------------------------------

!!REAL(KIND=JPRB) :: ZBUFF(KEND-KSTA+1,YDGEOMETRY%YRDIMV%NFLEVG)

INTEGER(KIND=JPIM) :: II,JN,JV,IIS,II0

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "mxture.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SPCSIDG_PART1',0,ZHOOK_HANDLE)

ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDLAP=>YDGEOMETRY%YRLAP,YDMP=>YDGEOMETRY%YRMP)
ASSOCIATE(NSMAX=>YDDIM%NSMAX,NFLEVG=>YDDIMV%NFLEVG,&
        & SIHEGIND=>YDDYN%SIHEGIND)

  IIS=2
  II0=1
  IF (.NOT. LDSIDG) THEN
    IIS=4
    II0=2
  ENDIF

  IF (KM > 0) THEN
     II=IIS
!               Inversion of a symmetric matrix.
     CALL MXTURE(NISNAX(KM)+1,KEND-KSTA+1,NFLEVG,NFLEVG,II,IIS,-2,1,&
     & SIHEG(SIHEGIND(KMLOC):SIHEGIND(KMLOC+1)-1,1),&
     & SIHEG(SIHEGIND(KMLOC):SIHEGIND(KMLOC+1)-1,2),&
     & SIHEG(SIHEGIND(KMLOC):SIHEGIND(KMLOC+1)-1,3),&
     & SIHEG(SIHEGIND(KMLOC):SIHEGIND(KMLOC+1)-1,2),&
     & SIHEG(SIHEGIND(KMLOC):SIHEGIND(KMLOC+1)-1,3),PSDIVP,PSPDIVP)!!,ZBUFF) 
  ELSE

  !               Inversion of a non-symmetric matrix.
    II=II0
!à voir!    II=2 sans mise à 0 après

    CALL MXTURE(NISNAX(KM)+1,KEND-KSTA+1,NFLEVG,NFLEVG,II,IIS,-2,3,&
     & SIHEG(SIHEGIND(KMLOC):SIHEGIND(KMLOC+1)-1,1),&
     & SIHEG(SIHEGIND(KMLOC):SIHEGIND(KMLOC+1)-1,2),&
     & SIHEG(SIHEGIND(KMLOC):SIHEGIND(KMLOC+1)-1,3),&
     & SIHEG2(:,:,2),&
     & SIHEG2(:,:,3),PSDIVP,PSPDIVP)!!,ZBUFF)  

    IF (LDSIDG) THEN
      !$OMP PARALLEL DO PRIVATE(JV,JN)
      DO JV=1,NFLEVG
        DO JN=0,2*NSMAX,2
          PSPDIVP(KSTA+JN+1,JV)=0.0_JPRB
        ENDDO
      ENDDO
      !$OMP END PARALLEL DO
    ELSEIF (IIS==4) THEN
      !$OMP PARALLEL DO PRIVATE(JN,JV)
      DO JV=1,NFLEVG
        DO JN=0,NISNAX(KM)-1
          PSPDIVP(KSTA+4*JN+3,JV)=0.0_JPRB
          PSPDIVP(KSTA+4*JN+4,JV)=0.0_JPRB
        ENDDO
      ENDDO
      !$OMP END PARALLEL DO
    ENDIF
  ENDIF

END ASSOCIATE
END ASSOCIATE

IF (LHOOK) CALL DR_HOOK('SPCSIDG_PART1',1,ZHOOK_HANDLE)
END SUBROUTINE SPCSIDG_PART1

