SUBROUTINE SPCSIDG_PART2TOT(YDGEOMETRY,KSTA,KEND,PSPDIVG,PHELP,&
   & KM,KMLOC,LDSIDG,KSZNISNAX,NISNAX,ZPD,ZPE,ZPF)

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1     , ONLY : JPIM, JPRB
USE YOMHOOK      , ONLY : LHOOK, DR_HOOK, JPHOOK
USE YOMMP0       , ONLY :MYSETN

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTA
INTEGER(KIND=JPIM),INTENT(IN)    :: KEND
INTEGER(KIND=JPIM),INTENT(IN)    :: KM !0 if IM=0 on this MPI task 
INTEGER(KIND=JPIM),INTENT(IN)    :: KMLOC !first KMLOC on this MPI task
LOGICAL           ,INTENT(IN)    :: LDSIDG
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSPDIVG(KSTA:KEND,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PHELP(KSTA:KEND,YDGEOMETRY%YRDIMV%NFLEVG)
INTEGER(KIND=JPIM),INTENT(IN)    :: KSZNISNAX
INTEGER(KIND=JPIM),INTENT(IN)    :: NISNAX(0:KSZNISNAX)
REAL(KIND=JPRB)   ,INTENT(IN)    :: ZPD(1:YDGEOMETRY%YRDIM%NSPEC)
REAL(KIND=JPRB)   ,INTENT(IN)    :: ZPE(1:YDGEOMETRY%YRDIM%NSPEC) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: ZPF(1:YDGEOMETRY%YRDIM%NSPEC)

INTEGER(KIND=JPIM) :: II, IS0, ILEN,IIS,II0,JV,JN,IM

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "mxptma.h"

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('SPCSIDG_PART2TOT',0,ZHOOK_HANDLE)

ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDLAP=>YDGEOMETRY%YRLAP)
ASSOCIATE(NSMAX=>YDDIM%NSMAX, NFLEVG=>YDDIMV%NFLEVG,&
     &NPTRMF=>YDGEOMETRY%YRMP%NPTRMF)
!     ------------------------------------------------------------------
 
IIS=2
II0=1
IF (.NOT. LDSIDG) THEN
  IIS=4
  II0=2
ENDIF

IS0=YDLAP%NSE0L(NPTRMF(MYSETN))
IM=YDLAP%MYMS(NPTRMF(MYSETN))
II=IIS
ILEN=KEND/2!ILEN=NSMAX-YDLAP%MYMS(KMLOC)+1
CALL MXPTMA(ILEN,NFLEVG,NFLEVG,II,IIS,ZPD(IS0+1),&
& ZPE(IS0+1),ZPF(IS0+1),ZPE(IS0+1),ZPF(IS0+1),&
& PSPDIVG,PHELP)

!!for KM=0 values with JI=2 are set to 0, as was the case in the original code
!!(in the 49t0 version of SPCSI, array ZSPDIVG was initialized to zero, and the
!!values with JI=2 for KM=0 were not computed)
IF (IM==0) THEN
  IF (LDSIDG) THEN
    DO JV=1,NFLEVG
      DO JN=0,2*NSMAX,2
        PHELP(KSTA+JN+1,JV)=0.0_JPRB
      ENDDO
    ENDDO
  ELSEIF (IIS==4) THEN
    DO JV=1,NFLEVG
      DO JN=0,NISNAX(KM)-1
        PHELP(KSTA+4*JN+3,JV)=0.0_JPRB
        PHELP(KSTA+4*JN+4,JV)=0.0_JPRB
      ENDDO
    ENDDO
  ENDIF
ENDIF

END ASSOCIATE
END ASSOCIATE

IF (LHOOK) CALL DR_HOOK('SPCSIDG_PART2TOT',1,ZHOOK_HANDLE)

END SUBROUTINE SPCSIDG_PART2TOT

